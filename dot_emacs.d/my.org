#+title: My Elisp Snippets

This file contains a collection of elisp functions which are ~org-babel-load-file~'d into my Emacs configuration.

* Dotfile Management
Here are some functions used to aid in managing my dotfiles with [[https://www.chezmoi.io/][Chezmoi]].

#+begin_src emacs-lisp
(defun my/chezmoi-apply ()
  "Apply chezmoi configuration."
  (interactive)
  (shell-command "chezmoi apply"))

(defvar my/chezmoi-dir (expand-file-name "~/.local/share/chezmoi/"))

(defun my/open-dotfiles ()
  "Open chezmoi in Dired."
  (interactive)
  (dired my/chezmoi-dir))
#+end_src

* Org Snippets
** Org <-> Markdown
Yank markdown from the kill-ring in ~org-mode~ format. Stolen from [[http://yummymelon.com/devnull/import-markdown-to-org-with-the-clipboard-in-emacs.html][here]].

#+begin_src emacs-lisp
(defun my/yank-markdown-as-org ()
  "Yank Markdown text as Org.

This command will convert Markdown text in the top of the `kill-ring'
and convert it to Org using the pandoc utility."
  (interactive)
  (save-excursion
    (with-temp-buffer
      (yank)
      (shell-command-on-region
       (point-min) (point-max)
       "pandoc -f markdown -t org --wrap=preserve" t t)
      (kill-region (point-min) (point-max)))
    (yank)))
#+end_src

And, the inverse. Stolen (with slight modifications) from [[https://mbork.pl/2021-05-02_Org-mode_to_Markdown_via_the_clipboard][here]].
#+begin_src emacs-lisp
(defun my/copy-org-as-markdown ()
  "Copy the region (in Org) to the system clipboard as Markdown."
  (interactive)
  (require 'ox-md)
  (if (use-region-p)
      (let* ((region (buffer-substring-no-properties
		              (region-beginning)
		              (region-end)))
	         (markdown (org-export-string-as region 'md t '(:with-toc nil))))
	    (gui-set-selection 'CLIPBOARD markdown))))
#+end_src

* Theme
#+begin_src emacs-lisp
(require 'solar)
(setq calendar-latitude 37)
(setq calendar-longitude -122)

(defun my/sun-up-p ()
  "Return t if the sun is currently up, otherwise nil."
  (let* ((solar-data (solar-sunrise-sunset (calendar-current-date)))
         (sunrise-decimal (car (car solar-data)))
         (sunset-decimal (car (nth 1 solar-data)))
         (current-time-decimal (+ (string-to-number (format-time-string "%H"))
                                  (/ (string-to-number (format-time-string "%M"))
                                     60.0))))
    (and (> current-time-decimal sunrise-decimal)
         (< current-time-decimal sunset-decimal))))

(defun my/select-theme-by-solar (light-theme dark-theme)
  "Return LIGHT-THEME if the sun is up, otherwise DARK-THEME."
  (if (my/sun-up-p)
      light-theme
    dark-theme))
#+end_src
