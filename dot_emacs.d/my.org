#+title: My Elisp Snippets

This file contains a collection of elisp functions which are ~org-babel-load-file~'d into my Emacs configuration.

* Dotfile Management
Here are some functions used to aid in managing my dotfiles with [[https://www.chezmoi.io/][Chezmoi]].

#+begin_src emacs-lisp
(defun my/chezmoi-apply ()
  "Apply chezmoi configuration."
  (interactive)
  (shell-command "chezmoi apply"))

(defvar my/chezmoi-dir (expand-file-name "~/.local/share/chezmoi/"))

(defun my/open-dotfiles ()
  "Open chezmoi in Dired."
  (interactive)
  (dired my/chezmoi-dir))
#+end_src

* Eshell
I like to be able to quickly switch between a buffer and an ~eshell~ buffer.
The following functions aid this workflow.

#+begin_src emacs-lisp
(defvar my/switch-to-eshell-hook nil
  "Hook for `my/switch-to-eshell`. Hook functions are called after switching to eshell.")

(defun my/switch-to-eshell ()
  "Switch to the existing *eshell* buffer or create one if it doesn't exist."
  (interactive)
  (if (get-buffer "*eshell*")
      (switch-to-buffer "*eshell*")
    (eshell))
  (run-hooks 'my/switch-to-eshell-hook))
#+end_src

** Eshell Commands
*** Bookmarks
Open a bookmark by cd'ing to it or find-file'ing it.

stolen from https://gist.github.com/kkatsuyuki/fa930411a86169c3bb1f03337d4af280
#+begin_src emacs-lisp
(defun pcomplete/eshell-mode/bm ()
  "Completion for `bm'"
  (pcomplete-here (bookmark-all-names)))

(defun eshell/bm (&rest args)
  "cd or open the given bookmark entry"
  (setq args (flatten-tree args))
  (let ((bookmark (car args)) filename)
    (if (setq filename (bookmark-get-filename bookmark))
        ;; If it points to a directory, change to it.
        (if (file-directory-p filename)
            (eshell/cd filename)
          ;; otherwise, just jump to the bookmark
          (bookmark-jump bookmark))
      (error "%s is not a bookmark" bookmark))))
#+end_src

* Org Snippets
** Copy Markdown to Org
Yank markdown from the kill-ring in ~org-mode~ format. Stolen from [[http://yummymelon.com/devnull/import-markdown-to-org-with-the-clipboard-in-emacs.html][here]].

#+begin_src emacs-lisp
(defun my/yank-markdown-as-org ()
  "Yank Markdown text as Org.

This command will convert Markdown text in the top of the `kill-ring'
and convert it to Org using the pandoc utility."
  (interactive)
  (save-excursion
    (with-temp-buffer
      (yank)
      (shell-command-on-region
       (point-min) (point-max)
       "pandoc -f markdown -t org --wrap=preserve" t t)
      (kill-region (point-min) (point-max)))
    (yank)))
#+end_src

* Theme
#+begin_src emacs-lisp
(require 'solar)
(setq calendar-latitude 37)
(setq calendar-longitude -122)

(defun my/sun-up-p ()
  "Return t if the sun is currently up, otherwise nil"
  (let* ((solar-data (solar-sunrise-sunset (calendar-current-date)))
         (sunrise-decimal (car (car solar-data)))
         (sunset-decimal (car (nth 1 solar-data)))
         (current-time-decimal (+ (string-to-number (format-time-string "%H"))
                                  (/ (string-to-number (format-time-string "%M"))
                                     60.0))))
    (and (> current-time-decimal sunrise-decimal)
         (< current-time-decimal sunset-decimal))))

(defun my/select-theme-by-solar (light-theme dark-theme)
  "Return LIGHT-THEME if the sun is up, otherwise DARK-THEME."
  (if (my/sun-up-p)
      light-theme
    dark-theme))
#+end_src
